import org.gradle.api.java.archives.internal.DefaultManifest

buildscript {
    repositories {
        mavenCentral()
        //Needed only for SNAPSHOT versions
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        // Should in hierarchy
        maven {
            name "extra-repo"
            url "$rootProject.projectDir/ex-lib"
        }
    }
    dependencies {
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.30.0"
        classpath "org.gradlex:extra-java-module-info:1.2.1"
    }
}

plugins {
    id 'com.lwohvye.java-conventions'
    id 'org.springframework.boot' // 主要是定义了这个，定义了SpringBoot的Version相关，并提供了application, bootJar, bootBuildImage这些Task
    // 在parent root执行Task，会同步执行sub project的Task，比如执行了bootJar，那会执行subPro的bootJar(若其中没有该Task会ignore)
    id 'signing'
    id 'io.codearte.nexus-staging' version '0.30.0'
}

ext.sharedManifest = manifest {
    attributes(
            'Developer': 'lWoHvYe',
            'Created-By': 'Gradle',
            'Built-By': System.properties['user.name'],
            'Build-Jdk-Spec': "${System.properties['java.version']}"
    )
} as DefaultManifest

ext { // 这个定义是可以传递的
    lombokVersion = "1.18.25"
}

subprojects {
    dependencies {}
    tasks.withType(Javadoc).tap {
        configureEach {
            failOnError(false)
            options.addStringOption('Xdoclint:none', '-quiet')
            options.addStringOption('encoding', 'UTF-8')
            options.addStringOption('charSet', 'UTF-8')
            // Add support for custom tag
            options.addStringOption('tag', 'date:a:Init Date:')
            options.addStringOption('tag', 'author:a:Major Contributor:')
        }
    }
}

tasks.named("bootJar") {
    mainClass = ''
}

tasks.withType(Checkstyle).configureEach {
    reports {
        xml.required = false
        html.required = true
        html.stylesheet resources.text.fromFile('config/xsl/checkstyle-custom.xsl')
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'unicorn'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Valentine Unicorn'
                packaging = 'pom'
                // optionally artifactId can be defined here
                description = 'A Spring Boot Project With Jpa JWT Security and so on'
                url = 'https://github.com/WHY-lWoHvYe/valentine-p2p.git'
                properties = [
                        myProp                : "chaste unicorn",
                        "spring-boot.version" : "3.0.0",
                        "project.core.version": "4.0.0"
                ]
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'lWoHvYe'
                        name = '王红岩(lWoHvYe)'
                        email = 'lWoHvYe@outlook.com'
                        url = 'https://www.lwohvye.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/WHY-lWoHvYe/valentine-p2p.git'
                    developerConnection = 'scm:git:ssh://github.com/WHY-lWoHvYe/valentine-p2p.git'
                    url = 'https://github.com/WHY-lWoHvYe/valentine-p2p/tree/main'
                    tag = 'unicorn-v4.0.0-SNAPSHOT'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

nexusStaging {
    serverUrl = "https://s01.oss.sonatype.org/service/local/"
    //required only for projects registered in Sonatype after 2021-02-24
//    packageGroup = "org.mycompany.myproject" //optional if packageGroup == project.getGroup()
//    stagingProfileId = "yourStagingProfileId" //when not defined will be got from server using "packageGroup"
}
